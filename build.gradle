plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.gradlex.extra-java-module-info' version '1.12'
    id 'org.beryx.jlink' version '3.1.1'
}
javafx {
    version = 21
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.media']
}
repositories {
    mavenCentral()
}
application {
    mainClass = 'gptui.GptUiMain'
    mainModule = 'GptUi.main'
}
dependencies {
    implementation 'com.google.code.gson:gson:2.13.1', 'com.vladsch.flexmark:flexmark-ext-tables:0.64.8',
            'ch.qos.logback:logback-classic:1.5.18', 'org.slf4j:jul-to-slf4j:2.0.17',
            'com.gluonhq:ignite-guice:1.2.2', 'com.google.inject:guice:7.0.0',
            "org.apache.lucene:lucene-core:$lucene9Version", "org.apache.lucene:lucene-analysis-common:$lucene9Version",
            "org.apache.lucene:lucene-queryparser:$lucene9Version", 'org.controlsfx:controlsfx:11.2.2'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version", 'org.assertj:assertj-core:3.27.6',
            "org.testfx:testfx-junit5:4.0.18", 'org.awaitility:awaitility:4.3.0', 'com.google.jimfs:jimfs:1.3.0',
            'org.mockito:mockito-core:5.18.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.13.4',
            "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
}
test {
    testLogging.showStandardStreams = true
    useJUnitPlatform()
}
extraJavaModuleInfo {
    automaticModule("com.vladsch.flexmark:flexmark", "flexmark")
    automaticModule("com.vladsch.flexmark:flexmark-util-ast", "flexmark.util.ast")
    automaticModule("com.vladsch.flexmark:flexmark-util-format", "flexmark.util.format")
    automaticModule("com.vladsch.flexmark:flexmark-util-builder", "flexmark.util.builder")
    automaticModule("com.vladsch.flexmark:flexmark-util-dependency", "flexmark.util.dependency")
    automaticModule("com.vladsch.flexmark:flexmark-util-html", "flexmark.util.html")
    automaticModule("com.vladsch.flexmark:flexmark-util-sequence", "flexmark.util.sequence")
    automaticModule("com.vladsch.flexmark:flexmark-util-collection", "flexmark.util.collection")
    automaticModule("com.vladsch.flexmark:flexmark-util-data", "flexmark.util.data")
    automaticModule("com.vladsch.flexmark:flexmark-util-misc", "flexmark.util.misc")
    automaticModule("com.vladsch.flexmark:flexmark-util-visitor", "flexmark.util.visitor")
    automaticModule("com.vladsch.flexmark:flexmark-ext-tables", "flexmark.ext.tables")
    automaticModule("com.vladsch.flexmark:flexmark-util", "flexmark.util")
    automaticModule("com.vladsch.flexmark:flexmark-util-options", "flexmark.util.options")
    automaticModule("org.awaitility:awaitility", "org.awaitility")
    automaticModule("org.osgi:org.osgi.core", "osgi.core")
    automaticModule("com.google.j2objc:j2objc-annotations", "j2objc.annotations")
    automaticModule("com.google.code.findbugs:jsr305", "jsr305")
    automaticModule("com.google.guava:failureaccess", "failureaccess")
    automaticModule("com.gluonhq:ignite-guice", "ignite.guice")
    automaticModule("com.gluonhq:ignite-common", "ignite.common")
    automaticModule("aopalliance:aopalliance", "aopalliance")
    automaticModule("com.google.guava:failureaccess", "failureaccess")
    automaticModule("com.google.guava:listenablefuture", "listenablefuture")
    automaticModule("com.google.j2objc:j2objc-annotations", "j2objc.annotations")
    automaticModule("org.codehaus.mojo:animal-sniffer-annotations", "animal.sniffer.annotations")
    automaticModule("javax.inject:javax.inject", "javax.inject")
}
jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'GptUi'
    }
    addExtraDependencies("javafx")
}
tasks.jlink.dependsOn tasks.test
tasks.jlink.mustRunAfter tasks.clean, tasks.test

def installDir = System.properties['user.home'] + '/installed/GptUI'
tasks.register('addIconToImage', Copy) {
    mustRunAfter tasks.jlink
    from layout.projectDirectory.dir('src/main/resources/gptui/model/file').file('icon.png')
    into layout.projectDirectory.dir('build/image/bin')
}
tasks.register('deleteInstalledApp', Delete) {
    mustRunAfter tasks.addIconToImage
    delete installDir
}
tasks.register('installLocally', Copy) {
    mustRunAfter tasks.deleteInstalledApp
    dependsOn 'clean', 'jlink', 'addIconToImage', 'deleteInstalledApp'
    from layout.buildDirectory.dir('image').get()
    into installDir
}
